;--------------------------
;  DEMONSTRATE MULTIPLEXING
;--------------------------
; THE FOLLOWING PROGRAMS DEMONSTRATE MULTIPLEXING
; THE DISPLAYS AND THE DEVELOPMENT OF A DISPLAY SCAN ROUTINE
; SIMILAR TO THE ONE USED IN THE MONITOR.
; EACH ROUTINE ADDS A SMALL IMPROVEMENT.


DISPLY  EQU     84H     ;DISPLAY SEGMENT LATCH
SCAN    EQU     85H     ;DISPLAY SCAN LATCH

;---------------
; MULTIPLEXING 1
;---------------
; THIS PROGRAM SIMPLY DEMONSTRATES THE IDEA OF
; MULTIPLEXING OR TIME SHARING.
;
	ORG     2100H

MULTI1  LD      A,06H           ;SEVEN SEG CODE FOR 1
	OUT     (DISPLY),A      ;PUT IT IN LATCH IC1
	LD      A,20H           ;TURN ON THE TRANSISTOR
	OUT     (SCAN),A        ;FOR DISPLAY LD1
	CALL    DELAY
;
	LD      A,5BH           ;SEVEN SEG CODE FOR 2
	OUT     (DISPLY),A      ;PUT IT IN LATCH IC1
	LD      A,10H           ;TURN ON THE TRANSISTOR
	OUT     (SCAN),A        ;FOR DISPLAY LD2
	CALL    DELAY
;
	LD      A,4FH           ;SEVEN SEG CODE FOR 3
	OUT     (DISPLY),A      ;PUT IT IN LATCH IC1
	LD      A,08H           ;TURN ON THE TRANSISTOR
	OUT     (SCAN),A        ;FOR DISPLAY LD3
	CALL    DELAY
;
	LD      A,66H           ;SEVEN SEG CODE FOR 4
	OUT     (DISPLY),A      ;PUT IT IN LATCH IC1
	LD      A,04H           ;TURN ON THE TRANSISTOR
	OUT     (SCAN),A        ;FOR DISPLAY LD4
	CALL    DELAY
;
	LD      A,6DH           ;SEVEN SEG CODE FOR 5
	OUT     (DISPLY),A      ;PUT IT IN LATCH IC1
	LD      A,02H           ;TURN ON THE TRANSISTOR
	OUT     (SCAN),A        ;FOR DISPLAY LD5
	CALL    DELAY
;
	LD      A,7DH           ;SEVEN SEG CODE FOR 6
	OUT     (DISPLY),A      ;PUT IT IN LATCH IC1
	LD      A,01H           ;TURN ON THE TRANSISTOR
	OUT     (SCAN),A        ;FOR DISPLAY LD6
	CALL    DELAY
	JP      MULTI1
;
; DELAY SUBROUTINE
;
DELAY   LD      DE,0240H        ;START WITH A CONSTANT
DELAY1  DEC     DE              ;SUBTRACT ONE FROM DE
	LD      A,E             ;OR E WITH D TO SEE
	OR      D               ;IF DE PAIR IS ZERO
	JP      NZ,DELAY1       ;DE NOT ZERO KEEP GOING
	RET
;---------------
; MULTIPLEXING 2
;---------------
; NOW USE ADDRESSES 2000H TO 2005H AS A DISPLAY
; BUFFER TO HOLD THE INFORMATION TO BE DISPLAYED :
; (YOU WILL NEED TO PUT SOMETHING IN THESE ADDRESSES
; OTHERWISE RUBBISH WILL BE DISPLAYED)
;
; AND NOW WE USE A ROTATE INSTRUCTION TO MOVE
; THE BIT ALONG WHICH TURNS ON THE APPROPRIATE
; TRANSISTOR FOR EACH DISPLAY.
;  HL    DISPLAY      C REGISTER
; 2000     LD6        0000 0001
; 2001     LD5        0000 0010
; 2002     LD4        0000 0100
; 2003     LD3        0000 1000
; 2004     LD2        0001 0000
; 2005     LD1        0010 0000
;
	ORG     2200H

MULTI2  LD      HL,2005H        ;HL HAS 2005 IN IT
	LD      C,20H           ;0010 0000

	LD      A,(HL)          ;GET LD1 DATA FROM BUFFER
	OUT     (DISPLY),A
	LD      A,C             ;A HAS 0010 0000
	OUT     (SCAN),A        ;TURN ON DISPLAY LD1
	CALL    DELAY
	DEC     HL              ;HL NOW HAS 2004
	RRC     C               ;C NOW HAS 0001 0000
;
	LD      A,(HL)          ;GET LD2 DATA FROM BUFFER
	OUT     (DISPLY),A
	LD      A,C             ;A HAS 0001 0000
	OUT     (SCAN),A        ;TURN ON DISPLAY LD2
	CALL    DELAY
	DEC     HL              ;HL NOW HAS 2003
	RRC     C               ;C NOW HAS 0000 1000
;
	LD      A,(HL)          ;GET LD3 DATA FROM BUFFER
	OUT     (DISPLY),A
	LD      A,C             ;A HAS 0000 1000
	OUT     (SCAN),A        ;TURN ON DISPLAY LD3
	CALL    DELAY
	DEC     HL              ;HL NOW HAS 2002
	RRC     C               ;C NOW HAS 0000 0100
;
	LD      A,(HL)          ;GET LD4 DATA FROM BUFFER
	OUT     (DISPLY),A
	LD      A,C             ;A HAS 0000 0100
	OUT     (SCAN),A        ;TURN ON DISPLAY LD4
	CALL    DELAY
	DEC     HL              ;HL NOW HAS 2001
	RRC     C               ;C NOW HAS 0000 0010
;
	LD      A,(HL)          ;GET LD5 DATA FROM BUFFER
	OUT     (DISPLY),A
	LD      A,C             ;A HAS 0000 0010
	OUT     (SCAN),A        ;TURN ON DISPLAY LD5
	CALL    DELAY
	DEC     HL              ;HL NOW HAS 2000
	RRC     C               ;C NOW HAS 0000 0001
;
	LD      A,(HL)          ;GET LD6 DATA FROM BUFFER
	OUT     (DISPLY),A
	LD      A,C             ;A HAS 0000 0001
	OUT     (SCAN),A        ;TURN ON DISPLAY LD6
	CALL    DELAY
	JP      MULTI2
;---------------
; MULTIPLEXING 3
;---------------
; HAVE A CLOSE LOOK AT MULTIPLEXING 2 ABOVE,
; THERE A ESSENTIALLY SIX IDENTICAL ROUTINES
; WHICH EACH TURN ON ONE DISPLAY.
; NOW WE CAN WRITE THE WHOLE ROUTINE IN A SMALL
; NUMBER OF INSTRUCTIONS BY ENCLOSING THE IDENTICAL
; ONES IN A LOOP.
; WE CAN ALSO USE THE C REGISTER TO TELL US WHEN
; ALL DISPLAYS HAVE BEEN DONE AS THE SCAN BIT IS
; SHIFTED OUT OF C INTO THE CARRY.

	ORG     2300H

MULTI3  LD      HL,2005H
	LD      C,20H
;
; DISPLAY DIGIT LOOP
;
SHOW    LD      A,(HL)          ;GET DIGIT FROM BUFFER
	OUT     (DISPLY),A      ;LATCH DIGIT
	LD      A,C             ;GET SCAN NUMBER
	OUT     (SCAN),A        ;TURN ON DISPLAY
	CALL    DELAY           ;LEAVE ON FOR A WHILE
	DEC     HL              ;POINT TO NEXT DIGIT DATA
	RRC     C               ;POINT TO NEXT DIGIT SCAN
;
; DONE ALL DIGITS?
;
	JP      NC,SHOW         ;DONE IT 6 TIMES?
	JP      MULTI3
;
; NOW HAVE A LOOK AT THE SCAND ROUTINE IN THE MONITOR
; AND SEE HOW IT COMPARES TO THE ABOVE.
; CAN YOU SEE WHAT THE TWO DELAYS AT SCAND2 AND SCAND3 DO?
; TRY CHANGING THE VALUES IN ONTIM (3FD6) AND OFTIM (3FD7)
; AND SEE WHAT HAPPENS.

	END
